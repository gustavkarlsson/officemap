plugins {
  id "base"
  id "com.moowork.node" version "0.10"
}

repositories {
  mavenCentral()
}

configurations {
  jruby
}

dependencies {
  jruby "org.jruby:jruby-complete:1.7.20"
}

ext {
  cachesDir = "${projectDir}/.caches"
  npmCacheDir = "${cachesDir}/npm"
  nodeModulesDir = "${projectDir}/node_modules"
  bowerCacheDir = "${cachesDir}/bower"
  bowerComponentsDir = "${projectDir}/bower_components"
  jrubyDir = "${projectDir}/.jruby"
  appDir = "${projectDir}/app"
  sassDir = "${appDir}/styles"
  imagesDir = "${appDir}/images"
  javascriptsDir = "${appDir}/scripts"
  fontsDir = "${appDir}/styles/fonts"
  indexFile = "${appDir}/index.html"

  serveDir = "${projectDir}/.tmp"
  cssDir = "${serveDir}/styles"
  generatedImagesDir = "${serveDir}/images"
}

node {
  version = "0.12.5"
  npmVersion = "2.11.2"
  download = true
}

task configureNpmCache(type: NpmTask) {
  description = "Configure the NPM cache"
  outputs.files file(npmCacheDir)

  args = [ "config", "set", "cache", npmCacheDir ]
}

task installNodeModules(type: NpmTask, dependsOn: configureNpmCache) {
  description = "Install Node.js modules"
  inputs.files file("package.json")
  outputs.files file("node_modules")

  args = [ "install" ]
}

task installBowerComponents(type: NodeTask, dependsOn: installNodeModules) {
  description = "Install bower components"
  inputs.files file("bower.json")
  outputs.files file(bowerComponentsDir)

  script = file("${nodeModulesDir}/bower/bin/bower")
  args = ["--config.storage.cache=${bowerCacheDir}/cache",
          "--config.storage.packages=${bowerCacheDir}/packages",
          "--config.storage.registry=${bowerCacheDir}/registry",
          "install"]
}

task wiredep(type: NodeTask, dependsOn: [installNodeModules, installBowerComponents]) {
  description = "Wire Bower dependencies to your source code"
  inputs.dir file(bowerComponentsDir)
  outputs.file file(indexFile)

  script = file("node_modules/wiredep/wiredep-cli.js")
  args = ["--src", indexFile,
          "--ignorePath", ".."]
}

task installCompass(type: JavaExec) {
  description = "Install compass using JRuby"
    outputs.dir file(jrubyDir)

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args = ["-S", "gem", "install",
            "-i", jrubyDir,
            "--no-rdoc", "--no-ri",
            "compass"]
}

task compileSass(type: JavaExec, dependsOn: [installCompass, installBowerComponents]) {
    description = "Compile SASS using compass"
    inputs.dir file(sassDir)
    inputs.dir file(imagesDir)
    inputs.dir file(javascriptsDir)
    inputs.dir file(fontsDir)
    inputs.dir file(bowerComponentsDir)
    outputs.dir file(cssDir)
    outputs.dir file(generatedImagesDir)

    environment "GEM_PATH", jrubyDir
    environment "PATH", "${jrubyDir}/bin"

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args = ["-S", "compass", "compile",
            "--sass-dir", sassDir,
            "--css-dir", cssDir,
            "--images-dir", imagesDir,
            "--javascripts-dir", javascriptsDir,
            "--fonts-dir", fontsDir,
            "--import-path", bowerComponentsDir,
            "--relative-assets", "--sourcemap"]
}

task watchSass(dependsOn: [installCompass, installBowerComponents]) {
  doFirst {
    Thread.start {
      project.javaexec {
        environment "GEM_PATH", jrubyDir
        environment "PATH", "${jrubyDir}/bin"

        classpath = configurations.jruby
        main = "org.jruby.Main"
        args = ["-S", "compass", "watch",
                "--sass-dir", sassDir,
                "--css-dir", cssDir,
                "--images-dir", imagesDir,
                "--javascripts-dir", javascriptsDir,
                "--fonts-dir", fontsDir,
                "--import-path", bowerComponentsDir,
                "--relative-assets", "--sourcemap"]
      }
    }
  }
}

task copySourceFiles(type: com.sun.org.apache.xalan.internal.xsltc.compiler.Copy, dependsOn: wiredep) {
  description = "Copy source files to serve directory"
  exclude "**/*.scss"
  from file(appDir)
  from (projectDir) {
    include "bower_components/**"
  }
  into file(serveDir)
}

task autoprefixer(type: NodeTask, dependsOn: [installNodeModules, copySourceFiles]) {
  description = "Add vendor prefixes to CSS"
  def cssFiles = fileTree(cssDir).include("**/*.css")
  inputs.files cssFiles
  outputs.files cssDir

  script = file("node_modules/postcss-cli/bin/postcss")
  args = ["--use", "autoprefixer",
          "--autoprefixer.browsers", "last 1 version",
          "--autoprefixer.map", "true",
          "--dir", cssDir]
  args += fileTree(cssDir).include("**/*.css")
}

task serve(type: NodeTask, dependsOn: [watchSass, autoprefixer]) {
  description = "Runs HTTP server to serve content"

  script = file("server.js")
  // TODO watch and copy on changes
}

task build() {
  // clean:dist?
  // wiredep
  // useminPrepare
  // concurrent:dist
  // autoprefixer
  // ngtemplates
  // concat
  // ngAnnotate
  // copy:dist
  // cdnify
  // cssmin
  // uglify
  // filerev
  // usemin
  // htmlmin

  // TODO implement gradle build
}

task test2() { // TODO Solve name collision with jetty plugin task
  // clean:server
  // wiredep
  // concurrent:test
  // autoprefixer
  // connect:test
  // karma

  // TODO implement gradle test
}

// TODO set up clean task
clean.delete << file("dist")
clean.delete << file(nodeModulesDir)
clean.delete << file(bowerComponentsDir)
clean.delete << file(cachesDir)
clean.delete << file(jrubyDir)
clean.delete << file(".sass-cache")
clean.delete << file(serveDir)
